version "4.7"

class ScarcityHandler : EventHandler
{
	enum ScarcityToggleFlags
	{
		STF_Ammo = 1,
		STF_AmmoBulk = 2,
		STF_Mags = 4,
		STF_AmmoBoxes = 8,
		STF_Supplies = 16,
		STF_Armors = 32,
		STF_EnemyDrops = 64
	}

	override void WorldThingSpawned(WorldEvent e)
	{
		if (level.MapName ~== "LOTSAGUN")
		{
			return;
		}

		double perc = clamp(frandom(scarcity_percentage_min, scarcity_percentage_max), 0, 1.0);
		int toggles = scarcity_toggles;

		if (e.Thing is 'HDMobBase' && toggles & STF_EnemyDrops)
		{
			if (perc == 0 || frandom(0.01, 1.0) <= 1.0 - perc)
			{
				HDMobBase(e.Thing).bHASDROPPED = true;
				return;
			}
		}

		if (level.time > 3)
		{
			return;
		}

		if (toggles & STF_Supplies && (e.Thing is 'PortableMedikit' || e.Thing is 'HDInjectorMaker') || toggles & STF_AmmoBoxes && e.Thing is 'HDAmBox')
		{
			if (perc == 0 || frandom(0.01, 1.0) <= 1.0 - perc)
			{
				e.Thing.Destroy();
			}
			return;
		}

		let am = HDAmmo(e.Thing);
		if (am)
		{
			let mag = HDMagAmmo(am);
			if (mag && mag.Mags.Size() > 0)
			{
				bool arm = mag is 'HDArmour';
				if (arm && toggles & STF_Armors || !arm && toggles & STF_Mags)
				{
					if (perc == 0)
					{
						mag.Destroy();
						return;
					}
					if (arm && mag.Mags[0] > 1000)
					{
						int realAmt = mag.Mags[0] - 1000;
						mag.Mags[0] -= realAmt * perc;
						return;
					}
					mag.Mags[0] = int(mag.Mags[0] * perc);
				}
			}
			else if (toggles & STF_Ammo)
			{
				if (perc == 0)
				{
					am.Destroy();
					return;
				}
				am.Amount = int(am.Amount * perc);
			}
			return;
		}
		
		if (toggles & STF_AmmoBulk)
		{
			let upkp = HDUPK(e.Thing);
			if (upkp && upkp.pickuptype is 'HDAmmo')
			{
				if (perc == 0)
				{
					upkp.Destroy();
					return;
				}
				upkp.amount = int(upkp.amount * perc);
				return;
			}
		}
	}
}